
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьФункциональныеОпции();
	ЗаполнитьДеревоМетаданных();
	
КонецПроцедуры

Процедура ЗаполнитьФункциональныеОпции()

	ТаблицаФО = РеквизитФормыВЗначение("ФункциональныеОпции");
	
	КонстантыМД = Метаданные.Константы;
	ЗначенияКонстант = Новый Соответствие;
	
	Для Каждого ОбъектФО Из Метаданные.ФункциональныеОпции Цикл
		Для Каждого ОбъектСостава Из ОбъектФО.Состав Цикл
			Если ОбъектСостава.Объект = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ПолноеИмя = ОбъектСостава.Объект.ПолноеИмя();
			МассивФрагментов = СтрРазделить(ПолноеИмя, ".");
			
			СтрокаФО = ТаблицаФО.Добавить();
			СтрокаФО.Объект = ПолноеИмя + "#";
			СтрокаФО.ИмяФО = ОбъектФО.Имя;
			СтрокаФО.Хранение = ОбъектФО.Хранение.ПолноеИмя();
			СтрокаФО.ФрагментыИмени = МассивФрагментов;
			СтрокаФО.нрОбъект = НРег(ПолноеИмя) + "#";
			СтрокаФО.нрИмяФО = НРег(ОбъектФО.Имя);
			
			Если КонстантыМД.Содержит(ОбъектФО.Хранение) Тогда
				ДействуетФО = ЗначенияКонстант[ОбъектФО.Хранение];
				Если ДействуетФО = Неопределено Тогда
					ДействуетФО = Константы[ОбъектФО.Хранение.Имя].Получить();
					ЗначенияКонстант[ОбъектФО.Хранение] = ДействуетФО;
				КонецЕсли;
				СтрокаФО.ДействуетФО = ДействуетФО;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ТаблицаФО, "ФункциональныеОпции");
	
КонецПроцедуры

Процедура ЗаполнитьДеревоМетаданных()
	
	ДеревоМД = РеквизитФормыВЗначение("ДеревоМетаданных");
	ДеревоМД.Строки.Очистить();
	
	ЕстьОтборМД = НЕ ПустаяСтрока(ОтборМетаданных);
	ЕстьОтборФО = НЕ ПустаяСтрока(ОтборФункциональнаяОпция);
	нрОтборМетаданных = НРег(ОтборМетаданных);
	нрОтборФункциональнаяОпция = НРег(ОтборФункциональнаяОпция);
	
	Для Каждого СтрокаФО Из ФункциональныеОпции Цикл
		
		Если ЕстьОтборМД И СтрНайти(СтрокаФО.нрОбъект, нрОтборМетаданных) = 0 Тогда
			Продолжить;
		КонецЕсли;
		Если ЕстьОтборФО И СтрНайти(СтрокаФО.нрИмяФО, нрОтборФункциональнаяОпция) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиМД = ДеревоМД.Строки;
		Для Каждого ЧастьИмени Из СтрокаФО.ФрагментыИмени Цикл
			СтрокаМД = СтрокиМД.Найти(ЧастьИмени, "Имя");
			Если СтрокаМД = Неопределено Тогда
				СтрокаМД = СтрокиМД.Добавить();
				СтрокаМД.Имя = ЧастьИмени;
			КонецЕсли;
			СтрокиМД = СтрокаМД.Строки;
		КонецЦикла;
		СтрокаМД.ЕстьФО = Истина;
		СтрокаМД.Объект = СтрокаФО.Объект;
		
	КонецЦикла;
	
	ДеревоМД.Строки.Сортировать("Имя", Истина);
	
	ЗначениеВРеквизитФормы(ДеревоМД, "ДеревоМетаданных");
	
	Элементы.ФункциональныеОпции.ОтборСтрок = Новый ФиксированнаяСтруктура("Объект", "##");
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоМетаданныхПриАктивизацииСтроки(Элемент)
	
	ТекущаяСтрока = Элемент.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ДеревоМетаданных.НайтиПоИдентификатору(ТекущаяСтрока);
	Элементы.ФункциональныеОпции.ОтборСтрок = Новый ФиксированнаяСтруктура("Объект", ?(ТекущиеДанные.ЕстьФО, ТекущиеДанные.Объект, "##"));
	Элементы.ФункциональныеОпции.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМетаданныхПриИзменении(Элемент)
	ЗаполнитьДеревоМетаданных();
КонецПроцедуры

&НаКлиенте
Процедура ОтборФункциональнаяОпцияПриИзменении(Элемент)
	ЗаполнитьДеревоМетаданных();
КонецПроцедуры
