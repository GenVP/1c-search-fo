
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТаблицаФО = РеквизитФормыВЗначение("ФункциональныеОпции");
	ДеревоМД = РеквизитФормыВЗначение("ДеревоМетаданных");
	
	КонстантыМД = Метаданные.Константы;
	ЗначенияКонстант = Новый Соответствие;
	
	Для Каждого ОбъектФО Из Метаданные.ФункциональныеОпции Цикл
		Для Каждого ОбъектСостава Из ОбъектФО.Состав Цикл
			Если ОбъектСостава.Объект = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ПолноеИмя = ОбъектСостава.Объект.ПолноеИмя();
			МассивФрагментов = СтрРазделить(ПолноеИмя, ".");
			СтрокиМД = ДеревоМД.Строки;
			Для Каждого ЧастьИмени Из МассивФрагментов Цикл
				СтрокаМД = СтрокиМД.Найти(ЧастьИмени, "Имя");
				Если СтрокаМД = Неопределено Тогда
					СтрокаМД = СтрокиМД.Добавить();
					СтрокаМД.Имя = ЧастьИмени;
				КонецЕсли;
				СтрокиМД = СтрокаМД.Строки;
			КонецЦикла;
			СтрокаМД.ЕстьФО = Истина;
			СтрокаМД.Объект = ПолноеИмя + "#";
			
			СтрокаФО = ТаблицаФО.Добавить();
			СтрокаФО.Объект = ПолноеИмя + "#";
			СтрокаФО.ИмяФО = ОбъектФО.Имя;
			СтрокаФО.Хранение = ОбъектФО.Хранение.ПолноеИмя();
			
			Если КонстантыМД.Содержит(ОбъектФО.Хранение) Тогда
				ДействуетФО = ЗначенияКонстант[ОбъектФО.Хранение];
				Если ДействуетФО = Неопределено Тогда
					ДействуетФО = Константы[ОбъектФО.Хранение.Имя].Получить();
					ЗначенияКонстант[ОбъектФО.Хранение] = ДействуетФО;
				КонецЕсли;
				СтрокаФО.ДействуетФО = ДействуетФО;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ДеревоМД.Строки.Сортировать("Имя", Истина);
	
	ЗначениеВРеквизитФормы(ДеревоМД, "ДеревоМетаданных");
	ЗначениеВРеквизитФормы(ТаблицаФО, "ФункциональныеОпции");
	
	Элементы.ФункциональныеОпции.ОтборСтрок = Новый ФиксированнаяСтруктура("Объект", "##");
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоМетаданныхПриАктивизацииСтроки(Элемент)
	
	ТекущаяСтрока = Элемент.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ДеревоМетаданных.НайтиПоИдентификатору(ТекущаяСтрока);
	Элементы.ФункциональныеОпции.ОтборСтрок = Новый ФиксированнаяСтруктура("Объект", ?(ТекущиеДанные.ЕстьФО, ТекущиеДанные.Объект, "##"));
	Элементы.ФункциональныеОпции.Обновить();
	
КонецПроцедуры
